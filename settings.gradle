pluginManagement {
    includeBuild('build-logic')
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version "${foojayResolverVersion}"
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }
    versionCatalogs {
        // Version catalog imported from nva-commons
        create("nvaCatalog") {
            from("com.github.bibsysdev:versions:${nvaCommonsVersion}")
        }

        // Local version catalog for libraries in nva-commons
        // This is defined programmatically to reference the version from gradle.properties
        create("nvaLibs") {
            version('nva', "${nvaCommonsVersion}")
            library("core", "com.github.bibsysdev", "core").versionRef("nva")
            library("testutils", "com.github.bibsysdev", "nvatestutils").versionRef("nva")
        }
    }
}

rootProject.name = 'nva-gradle-template'

include 'common'
include 'calculator'
