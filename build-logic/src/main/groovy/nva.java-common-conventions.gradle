plugins {
    id 'java'
    id 'jacoco'
    id 'com.diffplug.spotless'
}

repositories {
    mavenCentral()
}

dependencies {
    constraints {
        // Define dependency versions as constraints
        implementation 'org.apache.commons:commons-text:1.12.0'
    }
}

testing {
    suites {
        // Configure the built-in test suite
        test {
            // Use JUnit Jupiter test framework
            useJUnitJupiter('5.11.3')
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// Configure project-wide automatic formatting
spotless {
    java {
        toggleOffOn() // Ignores sections between `spotless:off` / `spotless:on`
        googleJavaFormat().reflowLongStrings().formatJavadoc(true).reorderImports(true)
    }

    groovyGradle {
        target '**/*.gradle'
        greclipse()
        leadingTabsToSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }

    format 'misc', {
        target '.gitignore', '.gitattributes', '.editorconfig'
        leadingTabsToSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
}

// Reformat project before build
tasks.named('build').configure {
    dependsOn('spotlessApply')
}

tasks.named('test').configure {
    dependsOn('spotlessApply')
}
