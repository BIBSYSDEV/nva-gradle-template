plugins {
    id 'java-library'
    id 'nva.formatting-conventions'
    id 'jacoco'
    id 'pmd'
    id 'net.ltgt.errorprone'
}

group = 'com.github.bibsysdev'

dependencies {
    // Add ErrorProne for static analysis during compilation
    annotationProcessor libs.errorprone
    testAnnotationProcessor libs.errorprone
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
        vendor = JvmVendorSpec.AMAZON
    }
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        // Suppress build failures from lint errors (remove this if possible)
        allErrorsAsWarnings = true
    }
}

pmd {
    toolVersion = '7.12.0'
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

tasks.named('test', Test) {
    useJUnitPlatform()
    failFast = false
    testLogging {
        events = ['skipped', 'passed', 'failed']
        showCauses = true
        exceptionFormat = "full"
    }
}

tasks.named("jacocoTestReport", JacocoReport) {
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.named('check') {
    dependsOn tasks.named('test')
}
