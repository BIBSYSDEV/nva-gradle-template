plugins {
    id 'nva.common-conventions'
    id 'java-library'
    id 'jacoco'
    id 'pmd'
}

group = 'com.github.bibsysdev'

dependencies {
    testImplementation libs.bundles.testing
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

pmd {
    toolVersion = '7.12.0'
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

tasks.named('test', Test) {
    useJUnitPlatform()
    failFast = false
    testLogging {
        events = ['skipped', 'passed', 'failed']
        showCauses = true
        exceptionFormat = "full"
    }
}

// Do not generate test reports for individual projects
tasks.named("jacocoTestReport", JacocoReport) {
    enabled = false
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.named('check') {
    dependsOn tasks.named('test')
}
